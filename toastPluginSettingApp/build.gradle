apply plugin: 'com.android.application'

dependencies {
    implementation("net.jcip:jcip-annotations:${jcipAnnotationVersion}")
    implementation("androidx.annotation:annotation:${androidAnnotationVersion}")
    implementation("androidx.appcompat:appcompat:${androidAppcompatVersion}")
    implementation("com.twofortyfouram:android-annotation:${twofortyfouramMonorepoLibVersion}")
    implementation("com.twofortyfouram:android-assertion:${twofortyfouramMonorepoLibVersion}")
    implementation("com.twofortyfouram:android-plugin-api-for-locale:${twofortyfouramMonorepoLibVersion}")
    implementation("com.twofortyfouram:android-plugin-client-sdk-for-locale:${twofortyfouramMonorepoLibVersion}")
    implementation("com.twofortyfouram:android-spackle:${twofortyfouramMonorepoLibVersion}")

    androidTestImplementation("com.twofortyfouram:android-test:${twofortyfouramMonorepoLibVersion}")
    androidTestImplementation("androidx.test:core:${androidTestCoreVersion}")
    androidTestImplementation("androidx.test:rules:${androidTestRulesVersion}")
    androidTestImplementation("androidx.test.espresso:espresso-core:${espressoVersion}")
    androidTestImplementation group:'androidx.test.ext', name: 'junit', version: "${androidTestJunit}"
}

android {
    compileSdkVersion Integer.parseInt(androidCompileSdkVersion)

    defaultConfig {
        minSdkVersion Integer.parseInt(androidMinSdkVersion)
        targetSdkVersion Integer.parseInt(androidTargetSdkVersion)
        versionCode Integer.parseInt(androidVersionCode)
        versionName androidVersionName

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        // Keep only the default and English localizations; which reduces app size since
        // dependencies contain localizations.
        // The last two are for support of pseudolocales in debug builds.
        resConfigs "en", "en-rUS", "en_XA", "ar_XB"
    }

    if (RELEASE_KEYSTORE_PATH && RELEASE_KEYSTORE_PASSWORD && RELEASE_KEY_ALIAS && RELEASE_KEY_ALIAS_PASSWORD) {
        signingConfigs {
            release {
                storeFile file(RELEASE_KEYSTORE_PATH)
                storePassword RELEASE_KEYSTORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_ALIAS_PASSWORD
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled Boolean.parseBoolean(isCoverageEnabled)
            applicationIdSuffix ".debug"
            pseudoLocalesEnabled true
            zipAlignEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-project.txt"

            shrinkResources true

            if (RELEASE_KEYSTORE_PATH && RELEASE_KEYSTORE_PASSWORD && RELEASE_KEY_ALIAS && RELEASE_KEY_ALIAS_PASSWORD) {
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
